/*******************************************************************************
 * Title: bit-counter.dsh
 * Authors: Jose Serna
 * Created: 14-04-2018
 * Last modified: 2024-07-24 Nancy Day
 *
 * Notes: Two bit counter model taken from Shahram's PhD thesis
 *
 *        Shahram Esmaeilsabzali. Perscriptive Semantics for Big-Step Modelling Languages.
 *        PhD thesis, University of Waterloo, David R. Cheriton School of Computer Science, 2011
 *        https://cs.uwaterloo.ca/~nday/pdf/theses/2011-esmaeilsabzali-phd-thesis.pdf
 *
 ******************************************************************************/

state Counter {
    env event Tk_ext {}
    event Tk_int {}
    event Done {}

    conc Bit0 {
        
        default state Bit00 {}
        state Bit01 {}

        trans T1 {
            from Bit00
            on Tk_ext
            goto Bit01
        }

        trans T2 {
            from Bit01
            on Tk_ext
            goto Bit00
            send Tk_int
        }
    }
    
    conc Bit1 {

        default state Bit10 {}
        state Bit11 {}
        
        trans T3 {
            from Bit10
            on Tk_int
            goto Bit11
        }

        trans T4 {
            from Bit11
            on Tk_int
            goto Bit10
            send Done
        }
    }
}




